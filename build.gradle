plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id "org.sonarqube" version "5.0.0.4638"
}

group = 'com.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    // JUnit 5 Core
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // Cucumber Dependencies
    testImplementation 'io.cucumber:cucumber-java:7.15.0'
    testImplementation 'io.cucumber:cucumber-junit-platform-engine:7.15.0'
    testImplementation 'org.junit.platform:junit-platform-suite'
}

test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

sonar {
    properties {
        property "sonar.projectKey", "TP7_Jenkins_Key"
    }
}

publishing {
    repositories {
        maven {
            // Jenkins will inject these variables
            url = uri(System.getenv('REPO_URL') ?: "http://localhost")
            credentials {
                username = System.getenv('REPO_USER')
                password = System.getenv('REPO_PASS')
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}